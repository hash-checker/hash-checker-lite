apply plugin: 'com.android.application'
apply from: 'checkstyle.gradle'
apply from: 'cpd.gradle'
apply from: 'jacoco.gradle'
apply from: 'utils.gradle'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    defaultConfig {
        applicationId 'com.smlnskgmail.jaman.hashcheckerlite'
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 9
        versionName '1.0.8'
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner 'com.smlnskgmail.jaman.hashcheckerlite.runner.AndroidJacocoTestRunner'

        resConfigs 'en',
                'de',
                'el',
                'es',
                'fa',
                'fr',
                'hu',
                'it',
                'iw',
                'ko',
                'nl',
                'pl',
                'ru',
                'sv',
                'zh-rCN',
                'vi',
                'ja'
    }
    signingConfigs {
        release {
            final KEYSTORE_PATH = 'RELEASE_KEYSTORE_PATH'
            final KEYSTORE_PASSWORD = 'RELEASE_KEYSTORE_PASSWORD'
            final KEY_ALIAS = 'RELEASE_KEY_ALIAS'
            final KEY_PASSWORD = 'RELEASE_KEY_PASSWORD'
            def keystoreProperties = new Properties()
            def keystorePropertiesFile = rootProject.file("local.properties")
            if (keystorePropertiesFile.exists()) {
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            }
            def hasSignConfigInProperties = propertiesHasSignConfig(
                    keystoreProperties,
                    KEYSTORE_PATH,
                    KEYSTORE_PASSWORD,
                    KEY_ALIAS,
                    KEY_PASSWORD
            )
            if (!hasSignConfigInProperties) {
                hasSignConfigInProperties = envHasSignConfig(
                        KEYSTORE_PATH,
                        KEYSTORE_PASSWORD,
                        KEY_ALIAS,
                        KEY_PASSWORD
                )
                if (hasSignConfigInProperties) {
                    keystoreProperties.put(
                            KEYSTORE_PATH,
                            System.getenv(KEYSTORE_PATH)
                    )
                    keystoreProperties.put(
                            KEYSTORE_PASSWORD,
                            System.getenv(KEYSTORE_PASSWORD)
                    )
                    keystoreProperties.put(
                            KEY_ALIAS,
                            System.getenv(KEY_ALIAS)
                    )
                    keystoreProperties.put(
                            KEY_PASSWORD,
                            System.getenv(KEY_PASSWORD)
                    )
                }
            }
            if (hasSignConfigInProperties) {
                try {
                    storeFile file(keystoreProperties[KEYSTORE_PATH])
                    storePassword keystoreProperties[KEYSTORE_PASSWORD]
                    keyAlias keystoreProperties[KEY_ALIAS]
                    keyPassword keystoreProperties[KEY_PASSWORD]
                } catch (ignore) {
                    throw new InvalidUserDataException(
                            "You should define sign keys in gradle.properties or int ENV."
                    )
                }
            } else if (System.getenv('CI')) {
                throw new InvalidUserDataException(
                        "You should define sign keys in gradle.properties or in ENV."
                )
            }
        }
    }
    buildTypes {
        debug {
            versionNameSuffix '-dev'
            minifyEnabled false

            testCoverageEnabled = true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt')

            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "distribute-version"
    productFlavors {
        thirdPartyStores {
            dimension "distribute-version"
            versionNameSuffix "-thirdParty"
        }
        googlePlay {
            dimension "distribute-version"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            resources.srcDirs = [
                    'src/main/resources',
                    'src/test'
            ]
        }

    }
    testOptions {
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def buildType = variant.buildType.name
            output.outputFileName = "hash-checker-lite_${buildType}.apk"
        }
    }
}

static boolean propertiesHasSignConfig(
        properties,
        path,
        password,
        keyAlias,
        keyPassword
) {
    def has = { key -> properties.containsKey(key) }
    return has(path) && has(password) && has(keyAlias) && has(keyPassword)
}

static boolean envHasSignConfig(
        path,
        password,
        keyAlias,
        keyPassword
) {
    def has = { key -> System.getenv().containsKey(key) }
    return has(path) && has(password) && has(keyAlias) && has(keyPassword)
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.preference:preference:1.1.1'

    // Dagger
    implementation 'com.google.dagger:dagger:2.34.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.34.1'
    androidTestAnnotationProcessor 'com.google.dagger:dagger-compiler:2.34.1'

    // Checkstyle
    checkstyle 'com.github.fartem:checkstyle-checks-java:1.1.0'
    checkstyle 'com.github.fartem:checkstyle-checks-android:1.2.3'

    // Unit tests
    testImplementation 'junit:junit:4.13.1'

    // Android tests
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'

    // Screenshots
    androidTestImplementation 'tools.fastlane:screengrab:2.0.0'

    // Play Core Library
    googlePlayImplementation 'com.google.android.play:core:1.10.0'
}
